buildscript {
	ext {
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.13-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

allprojects {
	group = 'com.kingpiggy.app'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}
}

subprojects {
	// Plugins
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.ewerk.gradle.plugins.querydsl'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	dependencies {
		// Spring
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-jdbc'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		developmentOnly 'org.springframework.boot:spring-boot-devtools'

		// Lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// MySql
		implementation 'mysql:mysql-connector-java:8.0.29'

		//QueryDSL
		implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
		implementation "com.querydsl:querydsl-apt:${queryDslVersion}"

		// Mybatis
		implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.4'

		// Test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
	}

	/**
	 * QueryDSL
	 */
	def querydslDir = "$buildDir/generated/querydsl"
	querydsl {
		jpa = true
		querydslSourcesDir = querydslDir
	}
	sourceSets {
		main.java.srcDir querydslDir
	}
	compileQuerydsl{
		options.annotationProcessorPath = configurations.querydsl
	}
	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
		querydsl.extendsFrom compileClasspath
	}

}

project(':core') {
	bootJar { enabled = false }
	jar { enabled = true }

	dependencies {

	}

}

project(':api-server') {
	dependencies {
		implementation project(":core")
	}
}

project(':batch-server') {
	dependencies {
		implementation project(":core")
		implementation 'org.springframework.boot:spring-boot-starter-batch'
		testImplementation 'org.springframework.batch:spring-batch-test'
	}
}
